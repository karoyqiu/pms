; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "{cm:PMS}"
#define MyAppVersion "0.1.0"
#define MyAppPublisher "Qiu Daolin"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
;AppId={{D6C6861C-0E9A-4DB5-8E2E-DE8365CB404F}
AppId={{5D548CF5-E1C4-4EDF-A737-F62B7F3A325C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\PMS
DefaultGroupName={#MyAppName}
OutputDir=output
OutputBaseFilename=PMS-{#MyAppVersion}-x86_64-offline
Compression=lzma
SolidCompression=yes
DisableDirPage=auto
ShowLanguageDialog=auto
DisableProgramGroupPage=auto
UninstallDisplayName={cm:PMS}
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
VersionInfoVersion={#MyAppVersion}
VersionInfoProductVersion={#MyAppVersion}
VersionInfoProductTextVersion={#MyAppVersion}
VersionInfoProductName=PMS
VersionInfoCompany={#MyAppPublisher}
VersionInfoDescription={#MyAppVersion}
MinVersion=0,6.1

[Languages]
Name: "en_US"; MessagesFile: "compiler:Default.isl"
Name: "zh_CN"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"

[CustomMessages]
en_US.PMS=Product Management System
zh_CN.PMS=产品管理系统
en_US.VisitPMS=Visit PMS
zh_CN.VisitPMS=访问产品管理系统
en_US.StartingService=Starting PMS web service...
zh_CN.StartingService=正在启动产品管理系统...

[Files]
Source: "node.exe"; DestDir: "{app}"; Flags: restartreplace uninsrestartdelete
Source: "nssm.exe"; DestDir: "{app}"; Flags: restartreplace uninsrestartdelete
Source: "..\index.js"; DestDir: "{app}\webroot"; Flags: ignoreversion
Source: "..\config\*"; DestDir: "{app}\webroot\config"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp
Source: "..\controllers\*"; DestDir: "{app}\webroot\controllers"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp
Source: "..\lib\*"; DestDir: "{app}\webroot\lib"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp
Source: "..\locales\*"; DestDir: "{app}\webroot\locales"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp
Source: "..\models\*"; DestDir: "{app}\webroot\models"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp
Source: "..\node_modules\*"; DestDir: "{app}\webroot\node_modules"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp
Source: "..\public\*"; DestDir: "{app}\webroot\public"; Flags: ignoreversion createallsubdirs recursesubdirs comparetimestamp

[Dirs]
Name: "{app}\log"; Flags: uninsalwaysuninstall
Name: "{app}\webroot\tmp"; Flags: uninsalwaysuninstall
Name: "{app}\webroot\.build"; Flags: uninsalwaysuninstall
Name: "{app}\webroot\public\images";

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\services\PMSService"; ValueName: "Description"; ValueType: String; ValueData: "PMS web service.";
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\services\PMSService\Parameters"; ValueName: "AppStopMethodSkip"; ValueType: Dword; ValueData: "$2";

[Run]
Filename: "{app}\nssm.exe"; Parameters: "install PMSService ""{app}\node.exe"" index.js"; AfterInstall: FixServiceSettings
Filename: "sc.exe"; Parameters: "start PMSService"; StatusMsg: "{cm:StartingService}"
Filename: "http://localhost:8000/"; Flags: shellexec postinstall skipifsilent nowait; Description: "{cm:VisitPMS}"

[InstallDelete]
Type: files; Name: "{app}\log\pms.log";

[UninstallRun]
Filename: "sc.exe"; Parameters: "stop PMSService";
Filename: "{app}\nssm.exe"; Parameters: "remove PMSService confirm"; RunOnceId: "RemovePMSService";

[ThirdParty]
UseRelativePaths=True

[Code]
procedure FixServiceSettings();
var
    app: String;
begin
    app := ExpandConstant('{app}');
    RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\PMSService',
        'DisplayName', 'PMS Service');
    RegWriteExpandStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\services\PMSService\Parameters',
        'AppDirectory', app + '\webroot');
end;
